/**
 * Gradle build config for GWT sdk
 *
 * @author lyhcode
 */

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'jetty'

// GWT Settings
def gwtVersion = '2.5.1'                // GWT SDK version
def gwtModule = 'edu.harvard.integer.client'  // GWT module name
def gwtStartupUrl = 'client.html'     // Startup URL for Dev Mode
def gwtWarPath = 'war'                  // WebContent directory

// lib-gwt-file
def libGwtFileVersion = '0.3.3'			// lib-gwt-file version
def lienzoCoreVersion = '1.2-RELEASE' 	// lienzo-core version
def smartGwtVersion = '2.4'				// SmartGWT version

def gwtServerHost = 'localhost'
def gwtServerPort = 8888

version = '1.0'

sourceSets.main.java.srcDirs = ['src']

// Jetty Web Container
//jettyRun.daemon = true                              // Specifies whether the Jetty server should run in the background.
jettyRun.contextPath = ''                           // The context path to use to deploy the web application.
jettyRun.webAppSourceDirectory = file(gwtWarPath)   // The directory containing the web application source files.
jettyRun.httpPort = gwtServerPort                   // The TCP port for Jetty to listen on for incoming HTTP requests.
jettyRun.scanIntervalSeconds = 5                    // The interval in seconds between scanning the web app for file changes.

// Package War
war {
	from gwtWarPath
}

repositories {
	mavenCentral()
	maven { url "https://oosnmp.net/dist/release" }
}

dependencies {
	providedCompile project(':common')
	providedCompile project(':server')
	compile ('org.jboss.spec.javax.servlet:jboss-servlet-api_3.1_spec:1.0.0.Final')
	
	compile ('org.jboss.spec.javax.servlet:jboss-servlet-api_3.1_spec:1.0.0.Final')
	runtime('org.jasig.cas.client:cas-client-core:3.1.12')
	compile ('org.jasig.cas.client:cas-client-core:3.1.12')
	
	// Compile GWT libs, needed for gwtCompile and the javaCompile
	// Also includes the servlet-api
	compile "com.google.gwt:gwt-user:${gwtVersion}"
	compile "com.google.gwt:gwt-dev:${gwtVersion}"
	compile "com.google.gwt:gwt-servlet:${gwtVersion}"

	// Needed for GWT compile and at runtime for RequestBuilder
	// Specify two artifacts as workaround for GRADLE-1934
	compile('javax.validation:validation-api:1.0.0.GA') {
		artifact {
			name = 'validation-api'
			type = 'jar'
		}
		artifact {
			name = 'validation-api'
			type = 'jar'
			classifier = 'sources'
		}
	}
	
	// Compile lib-gwt-file, needed for FileUpload
	compile "org.vectomatic:lib-gwt-file:${libGwtFileVersion}"
	
	// Compile lienzo-core, for graphics
	compile "com.emitrom:lienzo-core:${lienzoCoreVersion}"
	
	// SmartGWT
	compile "com.smartgwt:smartgwt:${smartGwtVersion}"
}

task gwtc (dependsOn: classes, type: JavaExec) {

	description = "GWT compile to JavaScript (production mode)"

	main = 'com.google.gwt.dev.Compiler'

	println "src path: " + sourceSets.main.java.srcDirs
	classpath {[
			sourceSets.main.java.srcDirs,
			configurations.compile,
			project(":common").sourceSets.main.java.srcDirs
		]}

	args = [
		gwtModule,
		'-war', gwtWarPath,
		'-style',  'PRETTY'
	]

	maxHeapSize = '512M'
}

task devmode (dependsOn: classes, type: JavaExec) {

	description = "Run development mode"

	main = 'com.google.gwt.dev.DevMode'

	classpath {[
			sourceSets.main.java.srcDirs,           // Java source
			sourceSets.main.output.resourcesDir,    // Generated resources
			sourceSets.main.output.classesDir,      // Generated classes
			sourceSets.main.compileClasspath,       // Deps
			configurations.compile,
			project(":common").sourceSets.main.java.srcDirs
		]}

	args = (gwtModule instanceof List)?gwtModule:[gwtModule]
	args += [
		'-startupUrl', "http://${gwtServerHost}:${gwtServerPort}/${gwtStartupUrl}",
		'-war', gwtWarPath
	]

	maxHeapSize = '256M'
}

javadoc {
   options.addStringOption("sourcepath", 'src')
}
